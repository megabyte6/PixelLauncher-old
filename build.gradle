plugins {
    // Apply the java plugin for better toolchain detection support.
    id 'java'
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    // Apply javafxplugin for JavaFX support.
    id 'org.openjfx.javafxplugin' version '0.0.13'
    // Apply jlink for building the app.
    id 'org.beryx.jlink' version '2.26.0'
    // Apply lombok.
    id 'io.freefair.lombok' version '8.0.1'
}

version = appVersion
def osName = System.getProperty("os.name")

ext {
    // Links for downloading jdks.
    jdkLinks = [
        'linux-aarch64': 'https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.6%2B10/OpenJDK17U-jdk_aarch64_linux_hotspot_17.0.6_10.tar.gz',
        'linux-x64'    : 'https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.6%2B10/OpenJDK17U-jdk_x64_linux_hotspot_17.0.6_10.tar.gz',
        'macos-aarch64': 'https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.6%2B10/OpenJDK17U-jdk_aarch64_mac_hotspot_17.0.6_10.tar.gz',
        'macos-x64'    : 'https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.6%2B10/OpenJDK17U-jdk_x64_mac_hotspot_17.0.6_10.tar.gz',
        'win-x64'      : 'https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.6%2B10/OpenJDK17U-jdk_x64_windows_hotspot_17.0.6_10.zip'
    ]
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation 'eu.iamgio:animated:0.7.0'
    implementation 'io.github.typhon0:AnimateFX:1.2.3'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'io.github.palexdev:materialfx:11.15.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainModule = 'org.pixellauncher'
    mainClass = 'org.pixellauncher.App'
}

javafx {
    version = javafxVersion
    modules = ['javafx.controls', 'javafx.fxml']
}

jlink {
    addOptions(
        '--strip-debug',
        '--compress', '2',
        '--no-header-files',
        '--no-man-pages'
    )

    // Remove the following as soon as the dependencies are fixed (upstream)
    // forceMerge is usually needed when some non-modular artifacts in the
    // dependency graph use code that was previously part of the JDK but it was
    // removed in the newer releases.
    // The pom.xml associated with such a non-modular artifact does not mention
    // that the artifact depends on the removed code (because the artifact was
    // published when this code was still available in the JDK).
    forceMerge('jackson', 'log4j-api')

    // Some libraries using JavaFX do not specify their JavaFX dependencies
    // because JavaFX was part of the JDK before being removed in Java 11.
    // Including addExtraDependencies("javafx") into the jlink block solves
    // this problem.
    // Can be removed once AnimateFX and animated become modular.
    addExtraDependencies("javafx")

    launcher {
        noConsole = true
    }
    imageZip = file("$buildDir/PixelLauncher.zip")

    if (jlinkTargetPlatform == 'all') {
        // Run all cases.
        jdkLinks.each { key, value ->
            targetPlatform(key) {
                jdkHome = jdkDownload(value)
                addExtraModulePath('jmods/20/' + key.split('-').join('/'))
            }
        }
    } else {
        targetPlatform(jlinkTargetPlatform) {
            jdkHome = jdkDownload(jdkLinks[jlinkTargetPlatform])
            addExtraModulePath('jmods/20/' + jlinkTargetPlatform.split('-').join('/'))
        }
    }

    jpackage {
        targetPlatformName = jpackageTargetPlatform
        imageName = 'Pixel Launcher'
        installerName = 'PixelLauncher'
        icon = 'src/main/resources/assets/icon.png'
        vendor = 'Brayden Chan'

        if (jpackageOutputType != 'default') {
            installerOptions = ["--type $jpackageOutputType"]
        } else {
            installerOptions = []
        }

        if (osName.toLowerCase().contains("windows")) {
            icon = 'src/main/resources/assets/icon.ico'
            installerOptions += [
                '--win-dir-chooser',
                '--win-menu',
                '--win-menu-group', 'Pixel Launcher',
                '--win-per-user-install',
                '--win-shortcut',
                '--win-shortcut-prompt',
                '--win-update-url', 'https://github.com/megabyte6/PixelLauncher/releases/latest'
            ]
        } else if (osName.toLowerCase().contains("linux")) {
            icon = 'src/main/resources/assets/icon.png'
            installerOptions += [
                '--linux-package-name', 'Pixel Launcher',
                '--linux-menu-group', 'Pixel Launcher',
                '--linux-shortcut'
            ]
        } else {
            icon = 'src/main/resources/assets/icon.png'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
